apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.vctapps.cloud_weatherview"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        applicationVariants.all {
            it.buildConfigField "String", "OPEN_WEATHER_API_KEY", "\"" + getOpenWeatherApiKey() + "\""
            it.buildConfigField "String", "OPEN_WEATHER_LAT", "\"" + getOpenWeatherLat() + "\""
            it.buildConfigField "String", "OPEN_WEATHER_LONG", "\"" + getOpenWeatherLong() + "\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def getLocalProperties() {
    Properties props = new Properties()
    props.load(project.rootProject.file('local.properties').newDataInputStream())
    return props
}

def getOpenWeatherApiKey() {
    Properties props = getLocalProperties()
    return props['openweather.apikey'].toString()
}

def getOpenWeatherLong() {
    Properties props = getLocalProperties()
    return props['openweather.lon'].toString()
}

def getOpenWeatherLat() {
    Properties props = getLocalProperties()
    return props['openweather.lat'].toString()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'com.google.android.things:androidthings:+'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:support-v4:$android_appcompat_version"

    //Koin
    implementation "org.koin:koin-android:$koin_version"

    //RxAndroid
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"
    implementation "io.reactivex.rxjava2:rxjava:$rx_java_version"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    api "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    api "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    //Tests
    testImplementation "junit:junit:$junit_version"
    testImplementation "com.squareup.okhttp:mockwebserver:$mock_webserver_version"
    testImplementation "org.mockito:mockito-core:$mockito_core_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_core_version"
}
